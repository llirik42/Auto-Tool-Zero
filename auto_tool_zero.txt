Const FEED_RATE = 400
Const THICKNESS = 8 
Const OFFSET = 20 
Const MAX_DEPTH = 25
Const EPSILON = 0.01
Const NO_TOUCH_ERROR = "Error"


Call main()


Sub main()
	originFeedRate = getCurrentFeedRate()
			
	setCurrentFeed(FEED_RATE)

	moveZUntillTouchOrReach(-MAX_DEPTH)
		
	If reachedMaxDepth() Then
		printMessage(NO_TOUCH_ERROR)
		moveFastZ(0)
	Else 
		setCurrentZ(THICKNESS)
		moveFastZ(OFFSET)       
	End If
		
	setCurrentFeed(originFeedRate)	
End Sub

Sub MoveZUntilltouchOrReach(z)
	Code "G31Z" & z
	While IsMoving
	Wend
End Sub

Sub moveFastZ(z)
	Code "G00Z" & z
End Sub

Function reachedMaxDepth()
	reachedMaxDepth = getCurrentZ() < -MAX_DEPTH + EPSILON
End Function

Function getCurrentFeedRate()
	getCurrentFeedRate = GetOEMDRO(818)
End Function

Sub setCurrentFeed(newFeed)
	Code "F"& newFeed
End Sub

Function getCurrentZ()
	getCurrentZ = GetOEMDRO(802)
End Function

Sub setCurrentZ(newZ)
	SetDro(2, newZ)
End Sub

Sub printMessage(message As String)
	Code "(" & message & ")"
End Sub      
